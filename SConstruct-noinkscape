import glob
import os

VariantDir('_build', src_dir='.')

env = Environment(ENV=os.environ)
inkscape = Builder(action = 'inkscape --without-gui --export-pdf=$TARGET $SOURCE')
env['BUILDERS']['Inkscape'] = inkscape
env['BUILDERS']['Latexdiff'] = Builder(action = 'latexdiff $SOURCES > $TARGET')
env['BUILDERS']['Copier'] = Builder(action = Copy('$TARGET', '$SOURCE'))

pdfs = [env.Copier(target = '_build/' + os.path.basename(pdf), source = pdf)
        for pdf in glob.glob('figures/*.pdf')]

main=env.PDF(target='_build/resume.pdf',source='resume.tex')

Depends(Flatten([main]),
        Flatten([pdfs]))

cont_build = env.Command('.continuous', 'resume.tex',
    'while :; do inotifywait -e modify $SOURCES; scons -Q; done')
Alias('continuous', cont_build)

Default(main)

env.Latexdiff(target='diff.tex',source=['versions/resume.v1.tex','resume.tex'])
diff = env.PDF(target='diff.pdf',source='diff.tex')
